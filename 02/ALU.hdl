// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a = x, b[0..15] = false, sel = zx, out = x1);
    Mux16(a = y, b[0..15] = false, sel = zy, out = y1);
    Not16 (in = x1, out = nx1);
    Not16 (in = y1, out = ny1);
    Mux16 (a = x1, b = nx1, sel = nx, out = x2);
    Mux16 (a = y1, b = ny1, sel = ny, out = y2);
    And16 (a = x2, b = y2, out = x2andy2);
    Add16 (a = x2, b = y2, out = x2py2);
    Mux16 (a = x2andy2, b = x2py2, sel = f, out = res);
    Not16 (in = res, out = nres);
    Mux16 (a = res, b = nres, sel = no, out[15] = t1, out[8..14] = t2,
    out[0..7] = t3);
    Or8Way(in[0] = t1, in[1..7] = t2, out = zr1);
    Or8Way(in = t3, out = zr2);
    Or (a = zr1, b = zr2, out = nzr);
    Not (in = nzr, out = zr);
    And (a = t1, b = t1, out = ng);
    And16 (a[0..7] = t3, a[8..14] = t2, a[15] = t1, b[0..7] = t3, b[8..14] =
    t2, b[15] = t1, out = out);


}